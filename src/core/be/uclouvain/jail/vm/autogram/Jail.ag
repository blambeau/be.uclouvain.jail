package be.uclouvain.jail.vm.autogram;

import net.chefbe.autogram.ast2.parsing.active.ActiveParser;

grammar Jail as jail extends ActiveParser {

	using be.uclouvain.jail.dialect.utils.UTILS as u;
	using be.uclouvain.jail.algo.graph.copy.match.GMatch as gm;
	
	/* -------------------------------------------------------------------------------------------- JAIL main commands */
	<jail:unit>                            ::= <u:spacing>? command:<jail:command u:list> <u:eof>
	                                         ; [u:node]
	<jail:command>                         ::= tor:<jail:systemc>
	                                         | tor:<jail:affectation> <';' u:symbol>
	                                         | tor:<jail:define> <';' u:symbol>
	                                         | tor:<jail:show> <';' u:symbol>
	                                         ; [u:return]
	<jail:systemc>                         ::= '\' name:<jail:identifier>
	                                                arg:<jail:systemarg u:list>?
	                                                <';' u:symbol>
	                                         | '\' name:<jail:identifier>
	                                                arg:<jail:systemarg u:list>?
	                                                <u:spacing>?
	                                         ; [u:node]
	<jail:systemarg>                       ::= value:<jail:identifier>
	                                         ; [u:node]
	<jail:affectation>                     ::= var:<jail:identifier> <'=' u:symbol>
	                                           value:<jail:gexpression>
	                                         ; [u:node]
	<jail:show>                            ::= value:<jail:gexpression>
	                                         ; [u:node]
	<jail:nativedoc>                       ::= <u:spaces>? doc:<jail:doc>+ <u:eof>
	                                         ; [u:node]

	/* -------------------------------------------------------------------------------------------- JAIL graph expression */
	<jail:gexpression>                     ::= tor:<jail:goperator>
	                                         | tor:<jail:gliteral>
	                                         | tor:<jail:varref>
	                                         ; [u:return]
	                                         
	// graph literals
	<jail:gliteral>                        ::= "<ยง" format:<jail:identifier>
	                                               literal:!"ยง>"?
	                                           <"ยง>" u:symbol>
	                                         ; [u:node]
		                                         
	// graph operator
	<jail:goperator>                       ::= '(' name:<jail:identifier>
	                                               operands:<jail:goperands>
	                                               options:<jail:options>?
	                                           <')' u:symbol>
	                                         ; [u:node]
	<jail:goperands>                       ::= operand:<jail:goperand u:list>
	                                         ; [u:node]
	<jail:goperand>                        ::= expr:<jail:gexpression>
	                                         | expr:<jail:literal>
	                                         | expr:<jail:varref>
	                                         ; [u:node]
	<jail:options>                         ::= option:<jail:option u:list>
	                                         ; [u:node]
	<jail:option>                          ::= ':' name:<jail:identifier> value:<jail:optvalue>
	                                         ; [u:node]
	<jail:optvalue>                        ::= tor:<jail:optmatch>
	                                         | tor:<jail:optliteral>
	                                         | tor:<jail:varref>
	                                         ; [u:return]
	<jail:optmatch>                        ::= match:<gm:match_do>
	                                         ; [u:node]
	<jail:optliteral>                      ::= value:<u:string>
	                                         | value:<u:boolean>
	                                         | value:<u:double>
	                                         | value:<u:integer>
	                                         ; [u:node]
	<jail:literal>                         ::= value:<u:string>
	                                         | value:<u:boolean>
	                                         | value:<u:double>
	                                         | value:<u:integer>
	                                         ; [u:node]

	/* -------------------------------------------------------------------------------------------- JAIL define */
	<jail:define>                          ::= DEFINE header:<jail:def_header>
	                                                  body:<jail:def_body>
	                                         ; [u:node]
	<jail:def_header>                      ::= '(' name:<jail:identifier>
	                                               operand:<jail:def_operands>
	                                               option:<jail:def_options>?
	                                           <')' u:symbol>
	                                         ; [u:node]
	<jail:def_operands>                    ::= placeholer:<jail:pholderdef u:list>
	                                         ; [u:node]
	<jail:def_options>                     ::= option:<jail:def_option u:list>
	                                         ; [u:node]
	<jail:def_option>                      ::= ':' name:<jail:identifier> placeholer:<jail:pholderdef>
	                                         ; [u:node]
	<jail:def_body>                        ::= op:<jail:goperator>
	                                         ; [u:node]
	
	/* -------------------------------------------------------------------------------------------- common utilities */
	<jail:pholderdef>                      ::= '<' name:<jail:identifier> <'>' u:symbol>
	                                         ; [u:node]
	<jail:varref>                          ::= <'(' u:symbol> var:<jail:identifier> <')' u:symbol>
	                                         | var:<jail:identifier>
	                                         ; [u:node]
	<jail:identifier>                      ::= tor:<u:identifier>
	                                         ; [u:return]

	/* -------------------------------------------------------------------------------------------- documentation */
	<jail:doc>                             ::= help:<u:javaDoc_comment> <u:spaces>
	                                           signature:<jail:signature> ';' <u:spaces>
	                                         ; [u:node]
	<jail:signature>                       ::= DEFINE NATIVE tor:!';'
	                                         ; [u:return]

}