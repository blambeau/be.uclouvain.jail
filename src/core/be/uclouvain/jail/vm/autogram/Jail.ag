package be.uclouvain.jail.vm.autogram;

import net.chefbe.autogram.ast2.parsing.active.ActiveParser;

grammar Jail as jail extends ActiveParser {

	using be.uclouvain.jail.dialect.commons.UTILS as u;
	using be.uclouvain.jail.algo.graph.copy.match.GMatch as gm;
	
	/* -------------------------------------------------------------------------------------------- JAIL main commands */
	<jail:unit>                            ::= <u:spacing>? command:<jail:command u:list> <u:eof>
	                                         ; [u:node]
	<jail:command>                         ::= tor:<jail:affectation>
	                                         | tor:<jail:show>
	                                         ; [u:return]
	<jail:affectation>                     ::= var:<jail:identifier> <'=' u:symbol>
	                                           value:<jail:gexpression> <';' u:symbol>
	                                         ; [u:node]
	<jail:show>                            ::= value:<jail:gexpression> <';' u:symbol>
	                                         ; [u:node]

	/* -------------------------------------------------------------------------------------------- JAIL graph expression */
	<jail:gexpression>                     ::= tor:<jail:goperator>
	                                         | tor:<jail:gliteral>
	                                         | tor:<jail:varref>
	                                         ; [u:return]
	                                         
	// graph literals
	<jail:gliteral>                        ::= "<ยง" format:<jail:identifier>
	                                               literal:!"ยง>"
	                                           <"ยง>" u:symbol>
	                                         ; [u:node]
		                                         
	// graph operator
	<jail:goperator>                       ::= '(' name:<jail:identifier>
	                                               operands:<jail:goperands>
	                                               options:<jail:options>?
	                                           <')' u:symbol>
	                                         ; [u:node]
	<jail:goperands>                       ::= operand:<jail:goperand u:list>
	                                         ; [u:node]
	<jail:goperand>                        ::= expr:<jail:gexpression>
	                                         | expr:<jail:literal>
	                                         ; [u:node]
	<jail:options>                         ::= option:<jail:option u:list>
	                                         ; [u:node]
	<jail:option>                          ::= ':' name:<jail:identifier> value:<jail:optvalue>
	                                         ; [u:node]
	<jail:optvalue>                        ::= tor:<jail:optmatch>
	                                         | tor:<jail:optliteral>
	                                         ; [u:return]
	<jail:optmatch>                        ::= match:<gm:match_do>
	                                         ; [u:node]
	<jail:optliteral>                      ::= value:<u:string>
	                                         | value:<u:boolean>
	                                         ; [u:node]
	<jail:literal>                         ::= value:<u:string>
	                                         | value:<u:boolean>
	                                         ; [u:node]

	/* -------------------------------------------------------------------------------------------- common utilities */
	<jail:varref>                          ::= <'(' u:symbol> var:<jail:identifier> <')' u:symbol>
	                                         ; [u:node]
	<jail:identifier>                      ::= tor:<u:identifier>
	                                         ; [u:return]

}